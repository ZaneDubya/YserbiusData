#include "defines.loh"
#include "flags.loh"

#define MONST_PRAETORIAN_A 25
#define MONST_PRAETORIAN_B 26
#define MONST_SHADAX 27
#define MONST_GUARDA_POST 4
#define MONST_GUARDA_WANDER 11
#define MONST_GUARDA_RELAX 3
#define MONST_GUARDB_POST 24
#define MONST_GUARDB_WANDER 22
#define MONST_HALL_DEATH_1 28
#define MONST_HALL_DEATH_2 29
#define MONST_HALL_DEATH_3 30
#define MONST_HALL_DEATH_4 31
#define MONST_HALL_DEATH_5 32
#define MONST_HALL_DEATH_6 33
#define MONST_HALL_DEATH_7 34
#define MONST_KEKEKETL 35
#define MONST_GUARDD_POST 16
#define MONST_GUARDD_WANDER 17
#define MONST_GAOLER 36
#define MONST_CHORONZAR_TOUGH 38
#define MONST_CHORONZAR_WIMPY 37
#define MONST_CHISS 39
#define BIG_BOOTY_1 14
#define BIG_BOOTY_2 39
#define BIG_BOOTY_3 76
#define BIG_BOOTY_4 134
#define BIG_BOOTY_5 171
#define BIG_BOOTY_6 40
#define BIG_BOOTY_7 92
#define BIG_BOOTY_8 171
#define BIG_BOOTY_9 61
#define BIG_BOOTY_10 16
#define BIG_BOOTY_11 166
#define BIG_BOOTY_12 24
#define BIG_BOOTY_13 171
#define BIG_BOOTY_14 136
#define BIG_BOOTY_15 59
#define BIG_BOOTY_16 90
#define BIG_BOOTY_17 171
#define BIG_BOOTY_18 17
#define NAOMI_PIC (23 + HUMANBARBARIAN - 1)
#define CONCIERGE_PIC (24 + HUMANBARBARIAN - 1)
#define SHADAX_KILLED_BIT 0x01
#define CHISS_KILLED_BIT 0x02
#define CHORONZAR_KILLED_BIT 0x04
#define GAOLER_KILLED_BIT 0x08
#define CHORONZAR_KNOWN_BIT 0x08
#define NAOMI_KNOWN_BIT 0x40
#define ALCHEMY_KNOWN_BIT 0x02
#define GOT_CHOR_EXP_BIT 0x01
#define ANOINTED_BIT 0x02
#define SECRET_PASSAGE_BIT 0x10
#define LEVER_PUZZLE 0
#define CONTROL_1 1
#define COASTER_STOPPING 2
#define STEAK_COUNT 3
#define DEATH_LEVEL 4
#define FISH_FEEDING_CNT 5
#define ALCHEMY_LEVEL 6
#define SIGNED_IN_BIT 0x01
#define POND_DESC_BIT 0x02
#define PASSWORD_FOUND_BIT 0x04
#define KEKEKETL_KILLED_BIT 0x08
#define FED_FISH_RIGHT_BIT 0x10
#define FED_FISH_LEFT_BIT 0x20
#define SEEN_IT 0
#define SEEN_IT2 1
#define DONE_IT 1
var alchemyStr = "Choronzar hopes to advance his position among the gods by magical means. To this end he is attempting an alchemical Binding Ritual on the mortal spirits his minions kidnap. Thus far success -- in the form of the correct formula -- has eluded him.";
var choronzarStr = "Choronzar is a trickster deity. Although he is a lesser light among the evil pantheon, his cunning and ambition far exceed the comprehension of his more powerful brethren, many of whom consider him insane and hence relatively harmless.";
var naomiStr = "Princess Naomi was abducted from her world by Shadax, who was attracted by the purity of her spirit. Choronzar fell in love with her and spared her from his experiments in the hope that she would return his affections. She has not.";
var dissStr = "The cauldron boils up as the bar dissolves.";
var brackStr = "The liquid in the cauldron turns a brackish gray-brown and stops bubbling.";
var anointStr = "'You have saved us from eternal torment. Our strength is now yours in the battle to come.'";
var piranhaDeathStr = "You are swarmed by a school of vicious piranhas and torn to shreds.";
var jostleStr = "You jostle each other and tumble into the water.";

fun SixMonst(context, monster) {
    var i;
    var num;
    switch (context.PartyCount) {
        case 1:
            num = 2;
            break;
        case 2:
            num = 3;
            break;
        case 3:
            num = 5;
            break;
        case 4:
        default: 
            num = 6;
            break;
    }
    for (i = 1; i <= num; i++) {
        context.AddEncounter(i, monster);
    }
}

fun MapEvent01(context) {
    var i;
    context.ShowText("The boiling lava scorches you terribly.");
    i = context.GetHealth - 1;
    context.DamagePlayer(i);
}

fun MapEvent02(context) {
    context.ShowText("You writhe in agony as the poisonous waters close over you");
    context.DamagePlayer(context.GetMaxHealth);
}

fun MapEvent03(context) {
    switch (context.GetTile) {
        case 4:
        case 6:
        case 8:
        case 10:
        case 68:
        case 70:
        case 72:
        default: 
            context.Teleport(8, 1, 0, EAST);
            break;
    }
}

fun MapEvent04(context) {
    if (context.GetFlag(DUNGEON, CHOR_NPC_KILLED) & CHORONZAR_KILLED_BIT) {
        context.Teleport(13, 1, 74, EAST);
    }
    else {
        context.Teleport(8, 1, 0, EAST);
    }
}

fun MapEvent05(context) {
    context.SetFloorBlock(context.GetTile);
}

fun MapEvent06(context) {
    context.Teleport(13, 1, 56, NORTH);
}

fun MapEvent07(context) {
    var fishFlag;
    var mapSquare;
    fishFlag = context.GetFlag(PARTY, FISH_FEEDING_CNT);
    mapSquare = context.GetTile;
    if ((mapSquare <= 148) || (fishFlag == 0)) {
        context.ShowText(piranhaDeathStr);
        context.DamagePlayer(context.GetMaxHealth);
        return;
    }
    switch (fishFlag) {
        case 0:
            context.ShowText(piranhaDeathStr);
            context.DamagePlayer(context.GetMaxHealth);
            break;
        case 1:
            context.ShowText("The piranhas have returned! They begin to snap and bite, mauling you horribly.");
            context.DamagePlayer(context.GetMaxHealth / 2);
            break;
        case 2:
            context.ShowText("The water to the north is less agitated than before.");
            break;
        case 3:
        default: 
            context.ShowText("Gingerly you wade through the pond. North of you the water continues to seethe where the piranhas are feeding.");
            break;
    }
    fishFlag--;
    context.SetFlag(PARTY, FISH_FEEDING_CNT, fishFlag);
}

fun MapEvent08(context) {
    context.NoJoinZone();
    if (context.PartyCount > 1) {
        context.ShowText("The coaster cars are only big enough for one of you at a time.");
        context.SetFlag(PARTY, COASTER_STOPPING, 0);
        context.Teleport(13, 1, 103, WEST);
    }
    switch (context.GetTile) {
        case 86:
            context.ShowText("The coaster gains speed as it dips giddily into the darkness.");
            context.Teleport(13, 2, 255, WEST);
            break;
        case 102:
            context.SetWallBlock(context.GetTile, NORTH);
            context.SetWallBlock(context.GetTile, SOUTH);
            if (context.IsFlagOn(PARTY, COASTER_STOPPING)) {
                context.ShowText("The coaster stops.");
                context.SetFlag(PARTY, COASTER_STOPPING, 0);
                context.Teleport(13, 1, 103, EAST);
            }
            else {
                if (context.HasItem(FUNHOUSEKEY)) {
                    context.ShowText("You twist the Funhouse Key in the keyhole. With a lurch the car begins to rumble forward along the rails.");
                    context.Teleport(13, 1, 86, NORTH);
                }
            }
            break;
        case 118:
        default: 
            context.SetFlag(PARTY, COASTER_STOPPING, 1);
            context.Teleport(13, 1, 102, NORTH);
            break;
    }
}

fun MapEvent09(context) {
    var flag;
    var square;
    var naomiFlag;
    flag = context.GetFlag(PARTY, CONTROL_1);
    square = context.GetTile;
    switch (square) {
        case 132:
            if (!(flag & POND_DESC_BIT)) {
                context.ShowText("You are standing on a small jetty above a fish pond. Before you a raised, earthen pathway snakes out across the water.");
                if (context.PartyCount > 1) {
                    context.ShowText("The path is narrow and slippery. You had better proceed alone.");
                }
                flag = flag | POND_DESC_BIT;
                context.SetFlag(PARTY, CONTROL_1, flag);
            }
            break;
        case 177:
            if (context.PartyCount > 1) {
                context.ShowText(jostleStr);
                context.Teleport(13, 1, square + 1, EAST);
            }
            if (context.GetFacing == SOUTH) {
                naomiFlag = context.GetFlag(DUNGEON, CHOR_NAOMI_MET);
                if ((naomiFlag == 1) || (naomiFlag == 2)) {
                    context.ShowText("To the south and east you can dimly make out the silhouette of a woman standing on a small island.");
                }
            }
            break;
        default: 
            if (context.PartyCount > 1) {
                context.ShowText(jostleStr);
                if ((square == 161) || (square == 146)) {
                    context.Teleport(13, 1, square + 1, EAST);
                }
                else {
                    context.Teleport(13, 1, square - 16, EAST);
                }
            }
            break;
    }
}

fun MapEvent0A(context) {
    var ctrlFlags;
    var cntFlag;
    if (context.UsedItem(JUICYDRAGONSTEAK, JUICYDRAGONSTEAK)) {
        context.TakeItem(JUICYDRAGONSTEAK);
        ctrlFlags = context.GetFlag(PARTY, CONTROL_1);
        cntFlag = context.GetFlag(PARTY, FISH_FEEDING_CNT);
        if (cntFlag == 0) {
            if (context.GetTile == 132) {
                ctrlFlags = ctrlFlags | FED_FISH_RIGHT_BIT;
            }
            else {
                ctrlFlags = ctrlFlags | FED_FISH_LEFT_BIT;
            }
            if ((ctrlFlags & FED_FISH_RIGHT_BIT) && (ctrlFlags & FED_FISH_LEFT_BIT)) {
                ctrlFlags = ctrlFlags & ~(FED_FISH_RIGHT_BIT | FED_FISH_LEFT_BIT);
                cntFlag = 6;
                context.ShowText("A swarm of fish churns the water in a mad feeding frenzy.");
            }
            else {
                context.ShowText("The nearby fish quickly gather and begin tearing at the steak.");
            }
        }
        else {
            cntFlag = 6;
        }
        context.SetFlag(PARTY, CONTROL_1, ctrlFlags);
        context.SetFlag(PARTY, FISH_FEEDING_CNT, cntFlag);
    }
    else {
        if (context.IsFlagOff(PARTY, FISH_FEEDING_CNT)) {
            context.ShowText("As you approach the water's edge you see the fish gathering expectantly.");
        }
    }
}

fun MapEvent0B(context) {
    if (context.IsFlagOff(PARTY, FISH_FEEDING_CNT)) {
        context.ShowText("Nasty-looking fish dart through the dark water below.");
    }
}

fun MapEvent0C(context) {
    if (context.GetFlag(PARTY, CONTROL_1) & SIGNED_IN_BIT) {
        context.ClearWallBlock(context.GetTile, EAST);
    }
    else {
        context.SetWallBlock(context.GetTile, EAST);
    }
}

fun MapEvent0D(context) {
    context.ShowText("Beneath some debris you find a water-damaged book entitled Journal of Bragadan the Brave. The only legible entry reads '...believe I have found a way through the accursed lever maze.");
    context.ShowText("Though I have struggled lifelong to uphold the right and good, now I must follow the sinister path...'");
}

fun MapEvent0E(context) {
    var switchCode;
    var strPtr1;
    var knownFlags;
    switchCode = context.GetTile;
    switchCode = switchCode * 256;
    switchCode = switchCode + context.GetFacing;
    knownFlags = context.GetFlag(DUNGEON, CHOR_KNOWN);
    switch (switchCode) {
        case 24067:
            if (knownFlags & ALCHEMY_KNOWN_BIT) {
                strPtr1 = alchemyStr;
            }
            break;
        case 24066:
            strPtr1 = choronzarStr;
            break;
        case 27648:
            if (knownFlags & NAOMI_KNOWN_BIT) {
                strPtr1 = naomiStr;
            }
            break;
        default: 
            break;
    }
    if (strPtr1 != NULL) {
        context.ShowText("Poring over the dusty tomes you learn the following:");
        context.ShowText(strPtr1);
    }
}

fun MapEvent0F(context) {
    var expFlag;
    var killedFlag;
    context.ShowText("This is a massive throne carved from a single block of polished obsidian.  It is inlaid with strange, beautiful sigils in a delicate silver filigree.");
    expFlag = context.GetFlag(DUNGEON, CHOR_CONTROL_1);
    killedFlag = context.GetFlag(DUNGEON, CHOR_NPC_KILLED);
    if ((killedFlag & CHORONZAR_KILLED_BIT) && (!(expFlag & GOT_CHOR_EXP_BIT))) {
        context.ShowText("You thrill to the surge of life force coursing into you from the throne.");
        context.AddExperience(10000000);
        expFlag = expFlag | GOT_CHOR_EXP_BIT;
        context.SetFlag(DUNGEON, CHOR_CONTROL_1, expFlag);
    }
}

fun MapEvent10(context) {
    context.ShowText("Embedded in the front panel of the car is a keyhole.");
}

fun MapEvent11(context) {
    context.ShowText("The glowing rift is the only way back to your world.");
    context.Teleport(8, 1, 0, EAST);
}

fun MapEvent12(context) {
    if (context.GetFlag(DUNGEON, CHOR_NAOMI_MET) >= 3) {
        context.ShowText("You discover a narrow secret passageway.");
        if (context.PartyCount > 1) {
            context.ShowText("You will have to squeeze through one at a time.");
        }
        context.SetFlag(DUNGEON, CHOR_CONTROL_1, context.GetFlag(DUNGEON, CHOR_CONTROL_1) | SECRET_PASSAGE_BIT);
        context.ClearWallBlock(context.GetTile, WEST);
    }
}

fun MapEvent13(context) {
    context.SetWallBlock(context.GetTile, context.GetFacing);
}

fun MapEvent14(context) {
    var switchFlag;
    var alchFlag;
    var takeItem = 0;
    var strPtr;
    context.SetFloorBlock(13);
    context.SetFloorBlock(14);
    context.SetFloorBlock(15);
    context.SetFloorBlock(31);
    context.SetFloorBlock(57);
    alchFlag = context.GetFlag(PARTY, ALCHEMY_LEVEL);
    context.ShowText("Alchemical implements of all descriptions clutter this workbench, including four large black kettles full of bubbling liquid.");
    context.SetFlag(DUNGEON, CHOR_KNOWN, context.GetFlag(DUNGEON, CHOR_KNOWN) | ALCHEMY_KNOWN_BIT);
    if (context.UsedItem(SILVERBAR, SILVERBAR)) {
        takeItem = SILVERBAR;
        if (alchFlag == 0) {
            alchFlag = 1;
            strPtr = dissStr;
        }
        else {
            strPtr = brackStr;
            alchFlag = 0;
        }
    }
    else {
        if (context.UsedItem(GOLDBAR, GOLDBAR)) {
            takeItem = GOLDBAR;
            if (alchFlag == 1) {
                alchFlag = 2;
                strPtr = dissStr;
            }
            else {
                strPtr = brackStr;
                alchFlag = 0;
            }
        }
        else {
            if (context.UsedItem(PLATINUMBAR, PLATINUMBAR)) {
                takeItem = PLATINUMBAR;
                if (alchFlag == 2) {
                    alchFlag = 3;
                    strPtr = dissStr;
                }
                else {
                    strPtr = brackStr;
                    alchFlag = 0;
                }
            }
            else {
                if (context.UsedItem(PRICELESSBAR, PRICELESSBAR)) {
                    takeItem = PRICELESSBAR;
                    if (alchFlag == 3) {
                        alchFlag = 4;
                        context.ShowText("Eerie green light floods the room as all of the cauldrons begin to boil violently. With an unearthly moan the spirits trapped within begin to stream forth in twisting, vaporous tendrils.");
                        strPtr = anointStr;
                    }
                    else {
                        strPtr = brackStr;
                        alchFlag = 0;
                    }
                }
            }
        }
    }
    if (takeItem != 0) {
        context.TakeItem(takeItem);
    }
    if (strPtr != NULL) {
        context.ShowText(strPtr);
    }
    context.SetFlag(PARTY, ALCHEMY_LEVEL, alchFlag);
}

fun MapEvent15(context) {
    var monster;
    var i;
    if (!(context.GetFlag(DUNGEON, CHOR_NPC_KILLED) & CHORONZAR_KILLED_BIT)) {
        switch (context.GetTile) {
            case 35:
                context.ShowText("Dazzled, you step into a brightly-lit, vaulted chamber. The musky smell of incense hangs in the air and the delicate music of chimes is audible in the distance.");
                monster = MONST_PRAETORIAN_A;
                context.ClearWallBlock(context.GetTile, WEST);
                break;
            case 36:
                if (context.GetFacing == EAST) {
                    context.ShowText("Ahead of you through the layers of smoke you see a massive golden throne.");
                }
                monster = MONST_PRAETORIAN_A;
                break;
            case 37:
                if (context.GetFacing == EAST) {
                    context.ShowText("As you draw closer you make out an insoucient figure lounging on the throne, dressed in jester's motley.");
                }
                monster = MONST_PRAETORIAN_B;
                break;
            case 38:
            default: 
                if (context.GetFacing == EAST) {
                    context.ShowText("The crazed figure on the throne stands, stares at you briefly, then prances off to the southeast, cackling like a lunatic. He dives into a pit and vanishes.");
                }
                monster = MONST_PRAETORIAN_B;
                break;
        }
        SixMonst(context, monster);
    }
}

fun MapEvent16(context) {
    var bootyFlag;
    var booty1 = 0;
    var booty2 = 0;
    var booty3 = 0;
    bootyFlag = context.GetFlag(DUNGEON, CHOR_BOOTY);
    switch (context.GetTile) {
        case 57:
            if (!(bootyFlag & 1)) {
                booty1 = BIG_BOOTY_1;
                booty2 = BIG_BOOTY_2;
                booty3 = BIG_BOOTY_3;
                bootyFlag = bootyFlag | 1;
            }
            break;
        case 58:
            if (!(bootyFlag & 2)) {
                booty1 = BIG_BOOTY_4;
                booty2 = BIG_BOOTY_5;
                booty3 = BIG_BOOTY_6;
                bootyFlag = bootyFlag | 2;
            }
            break;
        case 59:
            if (!(bootyFlag & 4)) {
                booty1 = BIG_BOOTY_7;
                booty2 = BIG_BOOTY_8;
                booty3 = BIG_BOOTY_9;
                bootyFlag = bootyFlag | 4;
            }
            break;
        case 91:
            if (!(bootyFlag & 16)) {
                booty1 = BIG_BOOTY_10;
                booty2 = BIG_BOOTY_11;
                booty3 = BIG_BOOTY_12;
                bootyFlag = bootyFlag | 16;
            }
            break;
        case 90:
            if (!(bootyFlag & 32)) {
                booty1 = BIG_BOOTY_13;
                booty2 = BIG_BOOTY_14;
                booty3 = BIG_BOOTY_15;
                bootyFlag = bootyFlag | 32;
            }
            break;
        case 89:
        default: 
            if (!(bootyFlag & 64)) {
                booty1 = BIG_BOOTY_16;
                booty2 = BIG_BOOTY_17;
                booty3 = BIG_BOOTY_18;
                bootyFlag = bootyFlag | 64;
            }
            break;
    }
    if (booty1 != 0) {
        context.ShowText("There is a pile of treasure here!");
        context.GiveItem(booty1);
        context.GiveItem(booty2);
        context.GiveItem(booty3);
    }
    context.SetFlag(DUNGEON, CHOR_BOOTY, bootyFlag);
}

fun MapEvent17(context) {
    if (context.UsedItem(context.GetEquippedWeapon, context.GetEquippedWeapon)) {
        context.ClearWallBlock(context.GetTile, NORTH);
        context.ShowText("You bash the lock off the door.");
    }
    else {
        context.ShowText("The door is held shut by a hefty padlock which does not appear to have a keyhole.");
    }
}

fun MapEvent18(context) {
    var steakCnt;
    steakCnt = context.GetFlag(PARTY, STEAK_COUNT);
    if ((!context.HasItem(JUICYDRAGONSTEAK)) && (steakCnt < 6)) {
        context.ShowText("You find a package of juicy dragon steaks!");
        steakCnt = steakCnt + 2;
        context.GiveItem(JUICYDRAGONSTEAK);
        context.GiveItem(JUICYDRAGONSTEAK);
    }
    context.SetFlag(PARTY, STEAK_COUNT, steakCnt);
}

fun MapEvent19(context) {
    context.ShowText("A small brass sign on the door reads 'Madcap Labs. Absolutely NO solicitors!'");
}

fun MapEvent1A(context) {
    context.NoJoinZone();
    if (context.PartyCount > 1) {
        context.ShowText("The passage is too narrow.");
        context.Teleport(13, 1, 12, WEST);
    }
    else {
        if (context.GetFlag(DUNGEON, CHOR_NAOMI_MET) < 3) {
            context.ShowText("You are not worthy.");
            context.Teleport(13, 1, 12, WEST);
        }
    }
}

fun MapEvent1C(context) {
    context.ShowText("Above the door is a wooden sign: 'Library. SHHHH!!!!'");
}

fun MapEvent1D(context) {
    if (context.UsedSkill(DETECT_SKILL) >= 12) {
        context.ShowText("You discover a secret compartment in the floor of the closet. A scroll reads 'Commander Shadax, You are to step up the acquisition of mortals for my experiments immediately. Signed, Choronzar'");
        context.SetFlag(DUNGEON, CHOR_KNOWN, context.GetFlag(DUNGEON, CHOR_KNOWN) | ALCHEMY_KNOWN_BIT);
    }
}

fun MapEvent1E(context) {
    var killedFlag;
    killedFlag = context.GetFlag(DUNGEON, CHOR_NPC_KILLED);
    if (context.HasItem(JESTERSCAP) && !(killedFlag & CHORONZAR_KILLED_BIT)) {
        killedFlag = killedFlag | CHORONZAR_KILLED_BIT;
        context.SetFlag(DUNGEON, CHOR_NPC_KILLED, killedFlag);
        context.ShowText("Choronzar's body begins to deliquesce as hissing smoke pours from his bloodied clothes. The room bucks and lurches as shockwaves roll through, knocking you to your knees.");
        context.ShowText("Over the din of the reverberations you hear a British voice asking 'what was that about HATS?' There is a sickening peal of hysterical, eldritch laughter. Then silence.");
    }
}

fun MapEvent1F(context) {
    context.ShowText("This is a freezer-locker.  Oversized blocks of ice stacked half-way to the ceiling keep it very cold in here.");
}

fun MapEvent21(context) {
    if (context.GetFlag(PARTY, ALCHEMY_LEVEL) == 4) {
        context.SetFlag(DUNGEON, CHOR_CONTROL_1, context.GetFlag(DUNGEON, CHOR_CONTROL_1) | ANOINTED_BIT);
    }
}

fun MapEvent32(context) {
    var flag;
    context.ShowText("On the south wall is a poster depicting a scantily-clad Kaalroth posing beside an enormous tankard of mead. Tearing it aside you discover the word 'SMILE' scrawled on the wall in charcoal.");
    flag = context.GetFlag(PARTY, CONTROL_1);
    flag = flag | PASSWORD_FOUND_BIT;
    context.SetFlag(PARTY, CONTROL_1, flag);
}

fun MapEvent33(context) {
    var passFlag;
    if (context.GetFlag(DUNGEON, CHOR_NPC_KILLED) & GAOLER_KILLED_BIT) {
        context.ClearWallBlock(context.GetTile, WEST);
    }
    else {
        context.ShowText("A slit in the door shoots open and a gutteral voice demands, 'Wot's th' password?'");
        passFlag = context.GetFlag(PARTY, CONTROL_1);
        if (passFlag & PASSWORD_FOUND_BIT) {
            context.ShowText("You reply 'the password is 'SMILE',' and the door creaks open. The gaoler growls, 'Come on in so's I can look at ye. One at a time!'");
            context.ClearWallBlock(context.GetTile, WEST);
        }
        else {
            context.ShowText("You stammer helplessly, not knowing the password. 'Come back when ye've remembered it!' The slit slams shut.");
        }
    }
}

fun MapEvent34(context) {
    var flag;
    flag = context.GetFlag(DUNGEON, CHOR_NAOMI_MET);
    if (flag == 0) {
        flag++;
        context.SetFlag(DUNGEON, CHOR_NAOMI_MET, flag);
        context.ShowPortrait(NAOMI_PIC);
        context.ShowText("You step into the dismal little cell and draw up short. Before you stands a radiant woman in a flowing, violet gown. She is slender and graceful but her eyes bore into you with unnerving intensity.");
        context.ShowText("'Enough!' she cries. 'I grow tired of these pathetic ruses. Tell your master my answer is 'NO!'' Before you can respond she stalks past you and is gone.");
        flag = context.GetFlag(DUNGEON, CHOR_KNOWN);
        flag = flag | NAOMI_KNOWN_BIT;
        context.SetFlag(DUNGEON, CHOR_KNOWN, flag);
    }
}

fun MapEvent35(context) {
    context.ShowText("Charts and war-trophies decorate the walls of this military command center. Several crude desks are strewn with scrolls and parchments.");
}

fun MapEvent36(context) {
    context.ShowText("This room, though spartan, appears to be the personal quarters of a high-ranking officer.");
}

fun MapEvent37(context) {
    context.ShowText("A portrait on the north wall depicts Shadax and a snake-woman posing in their wedding clothes. 'Shadax and Chiss, 8743 A.C.'");
}

fun MapEvent38(context) {
    context.ShowText("Before you gleams a wondrous, cunningly crafted chariot. Fashioned of hardwood and steel, it has been gaudily painted in glossy red and green lacquers. In front of the first car two shiny rails extend into the darkness to the north.");
}

fun MapEvent39(context) {
    var puzzFlag;
    context.ShowText("You pull the lever.");
    puzzFlag = context.GetFlag(PARTY, LEVER_PUZZLE);
    switch (context.GetTile) {
        case 87:
            if (puzzFlag == 0) {
                puzzFlag = 1;
            }
            else {
                puzzFlag = 0;
            }
            break;
        case 107:
            if (puzzFlag == 1) {
                puzzFlag = 2;
            }
            else {
                puzzFlag = 0;
            }
            break;
        case 153:
            if (puzzFlag == 2) {
                puzzFlag = 3;
            }
            else {
                puzzFlag = 0;
            }
            break;
        case 121:
            if (puzzFlag == 3) {
                context.SetWallItem(DOOR, 127, NORTH);
                context.ClearWallBlock(127, NORTH);
                puzzFlag = 4;
            }
            break;
        default: 
            puzzFlag = 0;
            break;
    }
    if (puzzFlag == 0) {
        context.ShowText("Mocking laughter echoes from far away.");
    }
    else {
        if (puzzFlag == 4) {
            puzzFlag = 0;
            context.ShowText("You hear a rumbling sound to the east.");
        }
    }
    context.SetFlag(PARTY, LEVER_PUZZLE, puzzFlag);
}

fun MapEvent3A(context) {
    context.ShowText("This is a storage room. Barrels and crates are stacked haphazardly.");
}

fun MapEvent3B(context) {
    var flagBits;
    context.ShowText("Buried beneath a stack of papers you find a scroll: 'Commander Shadax, It seems the princess prefers a gaol cell to the comforts of her chambers. Advise the Gaoler that she may come and go as she pleases. Signed, Choronzar'");
    flagBits = context.GetFlag(DUNGEON, CHOR_KNOWN);
    flagBits = flagBits | NAOMI_KNOWN_BIT;
    context.SetFlag(DUNGEON, CHOR_KNOWN, flagBits);
}

fun MapEvent64(context) {
    if (!context.HasItem(FUNHOUSEKEY)) {
        context.ShowText("A gigantic snake-woman looms over you, swaying hypnotically. 'Yessss,' she hisses, 'it will be sweet revenge to taste the mortal blood that stole my husband Shadax!'");
        context.SetTreasure(FUNHOUSEKEY, 0, 0, 0, 0, 0);
        context.AddEncounter(1, MONST_CHISS);
    }
}

fun MapEvent65(context) {
    var flagBits;
    flagBits = context.GetFlag(DUNGEON, CHOR_NPC_KILLED);
    if (!(flagBits & SHADAX_KILLED_BIT)) {
        context.ShowText("You feel a rush of vertigo as you pass through the rift.");
        context.ShowText("Before you towers an enormous Kaalroth in full battle dress. 'Who dares to invade the realm of Choronzar?', he roars. 'I, Shadax, shall see that you never return!'");
        flagBits = context.GetFlag(DUNGEON, CHOR_KNOWN);
        flagBits = flagBits | CHORONZAR_KNOWN_BIT;
        context.SetFlag(DUNGEON, CHOR_KNOWN, flagBits);
        context.AddEncounter(1, MONST_SHADAX);
    }
}

fun MapEvent67(context) {
    var flag;
    flag = context.GetFlag(DUNGEON, CHOR_NAOMI_MET);
    if ((flag > 0) && (flag < 3) && context.IsFlagOff(ROOM, SEEN_IT)) {
        context.ShowPortrait(NAOMI_PIC);
        context.ShowText("You clamber from the muddy water onto the island. Naomi is here.");
        context.SetFlag(ROOM, SEEN_IT, 1);
    }
    switch (flag) {
        case 1:
            context.ShowText("'Well,' she says icily, 'you are persistent in your mendacity, I grant you that.' She smiles slyly. 'If you really want me to believe you were not sent by Choronzar then find and return my Spirit Bottle!'");
            context.GiveItem(NAOMISKEY);
            flag++;
            break;
        case 2:
            if (context.UsedItem(SPIRITBOTTLE, SPIRITBOTTLE)) {
                context.TakeItem(SPIRITBOTTLE);
                context.ShowText("You offer the Spirit Bottle to Naomi. She snatches it from your hand and squints at it suspiciously. Then her eyes widen and her expression softens.");
                context.ShowText("'I have misjudged you,' she says quietly. 'My debt to you is so great that I can never hope to repay it. Still, I will do what I can.");
                context.ShowText("I once saw Choronzar slip through a secret passageway in the back of his laboratory. I believe it leads to his throne room!  Perhaps you can use it to circumvent his many guards.");
                context.ShowText("Now I must go. Farewell, and may Fortune smile upon you always!' She uncorks the bottle and a cloud of coruscating crimson fog quickly envelops her.  'I no longer exist here!'");
                context.ShowText("When the fog clears Naomi is gone.");
                flag++;
            }
            else {
                context.ShowText("Naomi snaps, 'Have you brought my Spirit Bottle, or is this another feeble attempt to deceive me?'");
                if (!context.HasItem(NAOMISKEY)) {
                    context.GiveItem(NAOMISKEY);
                }
            }
            break;
        default: 
            break;
    }
    context.SetFlag(DUNGEON, CHOR_NAOMI_MET, flag);
}

fun MapEvent68(context) {
    var flag;
    flag = context.GetFlag(PARTY, CONTROL_1);
    if (!(flag & SIGNED_IN_BIT)) {
        context.ShowPortrait(CONCIERGE_PIC);
        context.ShowText("An owlish man sits poring over some documents behind an imposing, oaken desk. Without glancing up he gestures toward a ledger-book. 'Sign in please.'");
        flag = flag | SIGNED_IN_BIT;
        context.SetFlag(PARTY, CONTROL_1, flag);
        context.SetFlag(PARTY, DEATH_LEVEL, 0);
    }
}

fun MapEvent69(context) {
    SixMonst(context, MONST_GUARDA_POST);
}

fun MapEvent6A(context) {
    SixMonst(context, MONST_GUARDA_WANDER);
}

fun MapEvent6B(context) {
    SixMonst(context, MONST_GUARDA_RELAX);
}

fun MapEvent6C(context) {
    SixMonst(context, MONST_GUARDB_POST);
}

fun MapEvent6D(context) {
    SixMonst(context, MONST_GUARDB_WANDER);
}

fun MapEvent6E(context) {
    var ctrlFlag;
    var deathFlag;
    var i;
    var monster;
    ctrlFlag = context.GetFlag(PARTY, CONTROL_1);
    deathFlag = context.GetFlag(PARTY, DEATH_LEVEL);
    if (context.IsFlagOff(ROOM, SEEN_IT) && (ctrlFlag & SIGNED_IN_BIT)) {
        context.ShowText("You trudge forward down the Hall of Death.");
        deathFlag++;
        context.SetFlag(ROOM, SEEN_IT, 1);
    }
    if (ctrlFlag & SIGNED_IN_BIT) {
        monster = (deathFlag / 3);
        if (monster > 6) {
            monster = 6;
        }
        monster = monster + MONST_HALL_DEATH_1;
        SixMonst(context, monster);
    }
    context.SetFlag(PARTY, DEATH_LEVEL, deathFlag);
}

fun MapEvent6F(context) {
    var flag;
    var i;
    var monster = MONST_HALL_DEATH_7;
    if (context.GetFlag(PARTY, CONTROL_1) & SIGNED_IN_BIT) {
        flag = context.GetFlag(PARTY, CONTROL_1);
        if (!(flag & KEKEKETL_KILLED_BIT)) {
            context.ShowText("This antechamber is littered with bones. As you step forward some of the grisly debris flies up and forms itself into a horrific, skeletal figure. 'I am Kekeketl,' it rasps. 'I am sworn to defend this place.'");
            flag = flag | KEKEKETL_KILLED_BIT;
        }
        else {
            context.ShowText("You advance, and once more Kekeketl rises from the carnage. 'I have told thee, I am sworn to defend this place.'");
        }
        context.SetFlag(PARTY, CONTROL_1, flag);
        context.AddEncounter(1, MONST_KEKEKETL);
        for (i = 2; i <= context.PartyCount + 1; i++) {
            context.AddEncounter(i, monster);
        }
    }
}

fun MapEvent70(context) {
    if (!(context.GetFlag(DUNGEON, CHOR_NPC_KILLED) & GAOLER_KILLED_BIT)) {
        if (context.PartyCount > 1) {
            context.ShowText("As you step through the door the Gaoler pushes you back roughly. 'One at a time!' he growls.");
            context.Teleport(13, 1, 249, WEST);
        }
        context.ShowText("As you step into the light the Gaoler flies into a rage. 'Wot's this then?' he cries. 'Oi've been tricked!'");
        context.AddEncounter(1, MONST_GAOLER);
    }
    context.ClearWallBlock(context.GetTile, EAST);
}

fun MapEvent71(context) {
    SixMonst(context, MONST_GUARDD_POST);
}

fun MapEvent72(context) {
    var secretFlag;
    var killedFlag;
    var monster;
    context.SetWallBlock(context.GetTile, WEST);
    secretFlag = context.GetFlag(DUNGEON, CHOR_CONTROL_1);
    killedFlag = context.GetFlag(DUNGEON, CHOR_NPC_KILLED);
    if ((secretFlag & SECRET_PASSAGE_BIT) && !(killedFlag & CHORONZAR_KILLED_BIT)) {
        context.ShowText("You rush through the wall and surprise Choronzar pacing behind his throne!");
        if (secretFlag & ANOINTED_BIT) {
            context.ShowText("He shrieks and turns to flee, but you are upon him in an instant.");
            monster = MONST_CHORONZAR_WIMPY;
        }
        else {
            context.ShowText("He smiles grimly. 'So it comes to this? % I hope you know what you're doing.'");
            monster = MONST_CHORONZAR_TOUGH;
        }
        context.SetTreasure(JESTERSCAP, 0, 0, 0, 0, 0);
        context.AddEncounter(1, monster);
    }
}

fun MapEvent73(context) {
    SixMonst(context, MONST_GUARDD_WANDER);
}

fun MapEvent74(context) {
    var flagBits;
    flagBits = context.GetFlag(DUNGEON, CHOR_NPC_KILLED);
    flagBits = flagBits | SHADAX_KILLED_BIT;
    context.SetFlag(DUNGEON, CHOR_NPC_KILLED, flagBits);
}

fun MapEvent75(context) {
    var flag;
    flag = context.GetFlag(DUNGEON, CHOR_NPC_KILLED);
    flag = flag | GAOLER_KILLED_BIT;
    context.SetFlag(DUNGEON, CHOR_NPC_KILLED, flag);
}
